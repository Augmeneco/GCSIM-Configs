#team config

#KurtC, KQMC: 10cr, 8cd, 2def%
itto char lvl=90/90 cons=0 talent=9,9,9;
itto add weapon="serpentspine" refine=1 lvl=90/90 +params=[stacks=5];
itto add set="husk" count=5; #running serpent means not prestacking husk
itto add stats hp=4780 atk=311 def%=0.583 geo%=0.466 cd=0.622; #mainstats
itto add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=0.2755 em=39.64 cr=0.3972 cd=0.5958; #substats

#KurtC, KQMC: exile with 5* cr circlet
gorou char lvl=90/90 cons=6 talent=9,9,9;
gorou add weapon="favoniuswarbow" refine=3 lvl=90/90;
gorou add set="theexile" count=4;
gorou add set="huskofopulentdreams" count=1;
gorou add stats hp=3571 atk=232 er=0.387 geo%=0.348 cr=0.311;
gorou add stats def%=0.0992 def=31.488 hp=406.304 hp%=0.07936 atk=26.464 atk%=0.07936 er=0.30856 em=31.712 cr=0.2648 cd=0.1324;
#exile gorou with a 5* CR% circlet:
#KQMC standard dictates x0.8 substat multiplier for 4* pieces, and -2 distributed substats per 4* piece (20-4x2=12)
#12 distributed substats left, 8 for CR% and 4 for ER% (of which 2 are put on the 5* circlet and are not affected by x0.8)
#out of the 20 fixed substats, 4 can be kept at 5* value (except CR% because mainstat), we keep 2 of ER% and CD% at 5* value

#KurtC, KQMC: 8cr, 10cd, 2atk%
zhongli char lvl=90/90 cons=0 talent=9,9,9;
zhongli add weapon="favoniuslance" refine=3 lvl=90/90;
zhongli add set="tenacity" count=5;
zhongli add stats hp=4780 atk=311 atk%=0.466 geo%=0.466 cr=0.311; #mainstats
zhongli add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.1984 er=0.1102 em=39.64 cr=0.331 cd=0.7944; #substats

#KurtC, KQMC: 8cr, 4cd, 8er
bennett char lvl=90/90 cons=6 talent=9,9,9;
bennett add weapon="thealleyflash" refine=1 lvl=90/90;
bennett add set="noblesseoblige" count=5;
bennett add stats hp=4780 atk=311 er=0.518 pyro%=0.466 cr=0.311; #mainstats
bennett add stats def%=0.124 def=39.36 hp=507.88 hp%=0.0992 atk=33.08 atk%=0.0992 er=1.0 em=39.64 cr=0.331 cd=0.3972; #substats

#enemy config
target lvl=100 resist=0.1 radius=2 pos=0,2.4 hp=999999999;
energy every interval=480,720 amount=1;

#sim config
options swap_delay=12 iteration=100;
active zhongli;

#action list

#ะก0 Itto:
if .itto.cons == 0 {
    zhongli skill[hold=1],dash;
    bennett skill,burst;
    gorou swap; wait(5); gorou burst;

    itto attack,burst[prestack=1],
        attack,skill[travel=0],
        attack:2,charge:5,
        attack:4,charge:5,skill[travel=0],charge;
        #"C0 1PS" combo
        wait(20); #catch ushi particles

    for let i = 0; i < 4; i = i + 1 {
        zhongli skill; #refreshing pillar
        gorou skill,burst;
        bennett skill,burst;
        zhongli skill[hold=1],dash;

        itto attack,burst[prestack=1],
            attack,skill[travel=0],charge:4,
            attack:3,charge:4,
            attack:3,skill[travel=0],charge:4;
            #"C0 2PS" combo
    }
}

#C1 Itto:
if .itto.cons >= 1 && .itto.cons < 6 {
    zhongli skill[hold=1],dash;
    bennett skill,burst;
    gorou swap; wait(5); gorou burst;

    itto attack,burst[prestack=1],
        attack,skill[travel=0],charge:7,
        attack:4,charge:5,skill[travel=0],charge;
        #"C1 1PS" combo
        wait(20); #catch ushi particles

    for let i = 0; i < 4; i = i + 1 {
        zhongli skill; #refreshing pillar
        bennett skill,burst;
        gorou skill,burst;
        zhongli skill[hold=1],dash;

        itto attack,burst[prestack=1],
            attack,skill[travel=0],charge:7,
            attack:4,charge:5,skill[travel=0],charge;
            #"C1 1PS" combo
            wait(20); #catch ushi particles
    }
}

#C6 Itto:
if .itto.cons == 6 {
    for let i=0; i<5; i=i+1 {
        if i == 0 {
            zhongli skill[hold=1], dash;
            bennett skill,burst;
            gorou swap; wait(5); gorou burst;
        }
        #rotation 1 setup

        else {
            zhongli skill;
            bennett skill,burst;
            gorou skill, burst;
            zhongli skill[hold=1], dash;
        }
        #rotation 2+ setup

        itto attack, burst, skill[travel=0];

        while !.itto.skill.ready {
            if .itto.tags.strStack > 0 { itto charge; }
            else { itto dash, attack; }
        }
        #manage itto C6 until second ushi

        itto skill[travel=0];

        while .itto.mods.itto-q {
            if .itto.tags.strStack > 0 { itto charge; }
            else { itto dash, attack; }
        }
        #manage itto C6 until Q expires

    }
}
